Value INTERFACE ([\w+/\.]+)
Value VRF (\S+)
Value STATUS (up|down)
Value IP ((\d+\.\d+\.\d+\.\d+)|--)
Value PEERIP (\d+\.\d+\.\d+\.\d+)
Value SPEED (\S+)
Value MTU (\d+)
Value VLAN ([\d+-]+)
Value TYPE (\S+)
Value MODE (routed|access|trunk|pvlan|fabric)
Value REASON (\S+((\s\w+)+)?)
Value PORTCH (\S+)
Value DESCRIPTION (\S+((\s\w+)+)?)
Value PROTOCOL (\S+)
Value VCID (\S+)

Start
  ^Port\s+VRF\s+Status\s+IP\s+Address\s+Speed\s+MTU -> Management 
  ^Ethernet\s+VLAN\s+Type\sMode\s+Status\s+Reason\s+Speed\s+Port\s*$$ -> Ethernet
  ^Interface\s+Ch\s+ -> Ethernet 
  ^Interface\s+Status\s+Description\s*$$ -> Loopback
  ^Interface\s+Secondary\s+VLAN\(Type\)\s+Status\s+Reason -> VLAN
  ^Port-channel\s+VLAN\s+Type\sMode\s+Status\s+Reason\s+Speed\s+Protocol -> PORTCHANNEL
  ^Port\s+Status\s+Reason\s+MTU\s+VRF -> MULTICAST
  ^Interface\s+Status\s+Peer\s+IP\s+Address\s+VC\s+ID -> PSEUDOWIRE
  ^---+$$
  ^. -> Error Start

Management
  ^${INTERFACE}\s+${VRF}\s+${STATUS}\s+${IP}\s+${SPEED}\s+${MTU} -> Record
  ^Ethernet\s+VLAN\s+Type\s+Mode\s+Status\s+Reason\s+Speed\s+Port -> Start
  ^---+$$
  ^\s*$$ -> Start
  ^. -> Error Management

Ethernet
  ^Interface\s+Ch\s+#\s*$$
  ^${INTERFACE}\s+${VLAN}\s+${TYPE}\s+${MODE}\s+${STATUS}\s+${REASON}\s+${SPEED}\s+${PORTCH} -> Record
  ^---+$$
  ^\s*$$ -> Start
  ^. -> Error Etherenet

Loopback
  ^${INTERFACE}\s+${STATUS}\s+${DESCRIPTION} -> Record
  ^---+$$
  ^\s*$$ -> Start
  ^. -> Error Loopback

VLAN
  ^${INTERFACE}\s+${TYPE}\s+${STATUS}\s+${REASON} -> Record
  ^---+$$
  ^\s*$$ -> Start
  ^. -> Error VLAN

PORTCHANNEL
  ^Interface\s*$$
  ^${INTERFACE}\s+${VLAN}\s+${TYPE}\s+${MODE}\s+${STATUS}\s+${REASON}\s+${SPEED}\s+${PROTOCOL} -> Record
  ^---+$$
  ^\s*$$ -> Start
  ^. -> Error PORTCHANNEL

MULTICAST
  ^${INTERFACE}\s+${STATUS}\s+${REASON}\s+${MTU}\s+${VRF} -> Record
  ^---+$$
  ^\s*$$ -> Start
  ^. -> Error MULTICAST

PSEUDOWIRE
  ^${INTERFACE}\s+${STATUS}\s+${PEERIP}\s+${VCID}
